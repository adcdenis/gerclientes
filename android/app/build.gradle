plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

android {
    namespace "com.gerclientes.app"
    compileSdk flutter.compileSdkVersion
    ndkVersion "27.0.12077973"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    signingConfigs {
        release {
            def keyProperties = new Properties()
            def keyPropertiesFile = rootProject.file("key.properties")
            if (keyPropertiesFile.exists()) {
                keyProperties.load(new FileInputStream(keyPropertiesFile))
            }
            keyAlias keyProperties['keyAlias']
            keyPassword keyProperties['keyPassword']
            storeFile file(keyProperties['storeFile'])
            storePassword keyProperties['storePassword']
        }
        debug {
            def keyProperties = new Properties()
            def keyPropertiesFile = rootProject.file("key.properties")
            if (keyPropertiesFile.exists()) {
                keyProperties.load(new FileInputStream(keyPropertiesFile))
            }
            def debugStore = keyProperties['storeFileDebug'] ?: keyProperties['storeFile']
            def debugAlias = keyProperties['keyAliasDebug'] ?: keyProperties['keyAlias']
            def debugStorePass = keyProperties['storePasswordDebug'] ?: keyProperties['storePassword']
            def debugKeyPass = keyProperties['keyPasswordDebug'] ?: keyProperties['keyPassword']
            keyAlias debugAlias
            keyPassword debugKeyPass
            storeFile file(debugStore)
            storePassword debugStorePass
        }
    }

    defaultConfig {
        applicationId "com.canutobr.gerclientes"
        minSdkVersion flutter.minSdkVersion
        targetSdk flutter.targetSdkVersion
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
        debug {
            // usa o mesmo keystore do release para que o SHA-1 seja idêntico
            signingConfig signingConfigs.release
        }
    }
}

flutter {
    source '../..'
}

// Renomeia o APK de release para o formato:
// NomeApp-<versao>-<AAAAmmdd_HHMMss>.apk
tasks.register('renameReleaseApk') {
    group = 'build'
    dependsOn 'assembleRelease'
    doLast {
        def manifestText = file('src/main/AndroidManifest.xml').getText('UTF-8')
        def m = (manifestText =~ /android:label="([^"]+)"/)
        def label = m.find() ? m.group(1) : 'App'

        def pubspecText = rootProject.file('../pubspec.yaml').getText('UTF-8')
        def vm = (pubspecText =~ /version:\s*([\w\.\-\+]+)/)
        def versionRaw = vm.find() ? vm.group(1) : '0.0.0+1'
        def version = versionRaw.split('\\+')[0]

        def timestamp = new java.text.SimpleDateFormat('yyyyMMdd_HHmmss').format(new Date())
        def newName = "${label}-${version}-${timestamp}.apk"

        // APK padrão do Gradle (módulo app)
        def outputsDir = new File(buildDir, 'outputs/apk/release')
        def standardApk = new File(outputsDir, 'app-release.apk')
        if (standardApk.exists()) {
            java.nio.file.Files.copy(
                standardApk.toPath(),
                new File(outputsDir, newName).toPath(),
                java.nio.file.StandardCopyOption.REPLACE_EXISTING
            )
        }

        // APK gerado pelo Flutter
        def flutterApkDir = new File(rootProject.projectDir, '../build/app/outputs/flutter-apk')
        def flutterApk = new File(flutterApkDir, 'app-release.apk')
        if (flutterApk.exists()) {
            java.nio.file.Files.copy(
                flutterApk.toPath(),
                new File(flutterApkDir, newName).toPath(),
                java.nio.file.StandardCopyOption.REPLACE_EXISTING
            )
        }
    }
}

afterEvaluate {
    tasks.findByName('assembleRelease')?.finalizedBy(tasks.findByName('renameReleaseApk'))
}
